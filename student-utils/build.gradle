buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:0.5.4.RELEASE'
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE")
    }
}

allprojects {
    group 'com.norulesweb.studenttracker.utils'

    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:all"
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom 'io.spring.platform:platform-bom:2.0.1.RELEASE'
        }
    }

    dependencies {
        compile("org.apache.commons:commons-lang3")
        compile("org.hibernate:hibernate-validator") // Bean validator is required by JPA if not running in a servlet container that provides this
        compile("org.glassfish:javax.el:3.0.0")      // javax.el implementation is required by JPA if not running in a servlet container that provides this
        compile("org.springframework.boot:spring-boot-starter-data-jpa")
        compile("org.springframework.boot:spring-boot-starter-integration")

        compile("org.apache.poi:poi:3.15")  // For reading Microsoft Office documents
        compile("org.apache.poi:poi-ooxml:3.15")

        compile project(":student-core")

        testCompile group: 'junit', name: 'junit', version: '4.11'
    }

}

project(":student-utils:core") {
    jar {
        baseName = "student-utils"
    }

    dependencies {
        compile("org.apache.commons:commons-collections4:4.1")
    }
}

configure( [
        project(":student-utils:data-load")
] ) {
    apply plugin: 'spring-boot'

    dependencies {
        compile("commons-cli:commons-cli:1.3.1")
        compile project(":student-utils:core")
    }
}


project(":student-utils:data-load") {
    jar {
        baseName = "student-utils-data-load"
    }
}
