buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:0.5.4.RELEASE'
        classpath 'mysql:mysql-connector-java:5.1.36' // For Liquibase
    }
}

plugins {
    id 'org.liquibase.gradle' version '1.2.1'
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'io.spring.platform:platform-bom:2.0.1.RELEASE'
    }
}


group 'com.norulesweb.studenttracker'


sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    querydslapt
}


dependencies {
    compile("com.migesok:jaxb-java-time-adapters:1.1.3")        // Date and time XML adapters
    compile("org.apache.commons:commons-lang3")
    compile("javax.validation:validation-api")
    compile("org.springframework.data:spring-data-jpa")
    compile("org.springframework.security:spring-security-core")
    compile("org.hibernate:hibernate-entitymanager")
    compile("org.hibernate:hibernate-validator")
    compile "com.mysema.querydsl:querydsl-core"
    compile "com.mysema.querydsl:querydsl-jpa"                  // Querydsl support JPA
    querydslapt "com.mysema.querydsl:querydsl-apt"              // Querydsl support annotation processing
    //compile("com.h2database:h2")
    compile("com.fasterxml.jackson.core:jackson-databind")
    compile("mysql:mysql-connector-java")
    compile("org.jadira.usertype:usertype.core:4.0.0.GA")
    compile("org.jadira.usertype:usertype.extended:4.0.0.GA")

    // compile project(":smartflow-event")

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

apply from: "liquibase.gradle"

///// Querydsl support
sourceSets {
    generated {
        java {
            srcDirs = ['src/main/generated']
        }
    }
}


task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
    dependsOn generateQueryDSL
    source generateQueryDSL.destinationDir
    options.compilerArgs << "-Xlint:all"
}

compileGeneratedJava {
    dependsOn generateQueryDSL
    options.warnings = false
    classpath += sourceSets.main.runtimeClasspath
}

clean {
    delete sourceSets.generated.java.srcDirs
}

idea {
    module {
        sourceDirs += file('src/main/generated')
    }
}
///// End Querydsl support
